group 'ledger-u2f-javacard'
version '1.0-SNAPSHOT'

// Buildscript configuration for the javacard-gradle plugin.
// Do not modify this particular block. Dependencies for the project are lower.
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.klinec:gradle-javacard:1.5.5'
    }
}

apply plugin: 'javacard'
apply plugin: 'jacoco'
sourceCompatibility = 1.8

// Common settings, definitions
final def rootPath = rootDir.absolutePath
final def libs = rootPath + '/libs'
final def libsSdk = rootPath + '/libs-sdks'

// Repositories for your project
repositories {
    mavenCentral()
    // mavenLocal() // for local maven repository if needed
    flatDir {
        dirs libs
    }
}

// Dependencies for your project
dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'

    jcardsim name: 'jcardsim-3_0_5/jcardsim-3.0.5-SNAPSHOT'
}

test {
    jvmArgs '-noverify'
}

// JavaCard SDKs and libraries
final def JC212 = libsSdk + '/jc212_kit'
final def JC221 = libsSdk + '/jc221_kit'
final def JC222 = libsSdk + '/jc222_kit'
final def JC303 = libsSdk + '/jc303_kit'
final def JC304 = libsSdk + '/jc304_kit'
final def JC305 = libsSdk + '/jc305u1_kit'

// Which JavaCard SDK to use - select
final def JC_SELECTED = JC305

javacard {

    //noinspection GroovyAssignabilityCheck
    config {
        jckit JC_SELECTED

        // JCardSim automatically added by the javacard-gradle plugin
        addSurrogateJcardSimRepo false
        addImplicitJcardSim false
        addImplicitJcardSimJunit false

        //noinspection GroovyAssignabilityCheck
        cap {
            packageName 'com.ledger.u2f'
            version '1.1'
            aid '0xa0:0x00:0x00:0x06:0x17:0x00:0x4f:0x97:0xa2:0xe9:0x50:0x01'
            output 'ledger-u2f.cap'

            //noinspection GroovyAssignabilityCheck
            applet {
                className 'com.ledger.u2f.U2FApplet'
                aid '0xa0:0x00:0x00:0x06:0x17:0x00:0x4f:0x97:0xa2:0xe9:0x49:0x01'
            }
        }
    }
}

